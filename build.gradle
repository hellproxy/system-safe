plugins {
    id 'idea'
}

ext {
    jupiterVersion = '5.6.0'
    mockitoVersion = '3.7.7'
    slf4jVersion = '1.7.30'
    junitTestKitVersion = '1.7.1'
    assertJVersion = '3.19.0'
    lombokVersion = '1.18.16'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    jacoco {
        toolVersion = "0.8.3"
    }

    test {
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test

        reports {
            html.enabled true
            xml.enabled true
            csv.enabled true
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    group 'com.github.twofour'
    version '1.0.0'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
    executionData.setFrom files(subprojects.jacocoTestReport.executionData)

    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
    }
}

build {
    finalizedBy jacocoRootReport
}
